MODULE DecorationFinal
!***************************************************************
! Decoration Movement Code for Robot 
! Author: Dylan Dam z5115759
!***************************************************************

    
    ! Custom waypoints that I have selected
    CONST robtarget ImmediatePoint1:=[[430,125,450],[0,0,1,0],[0,-1,0,0],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];
    CONST robtarget ImmediatePoint2:=[[265,180,450],[0,0,1,0],[0,-1,0,0],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];
    CONST robtarget ImmediatePoint3:=[[0,409,450],[0,0,1,0],[1,-1,1,0],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];
    CONST robtarget ImmediatePoint4:=[[265,-180,450],[0,0,1,0],[1,-1,1,0],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];
    CONST robtarget ImmediatePoint5:=[[430,-125,450],[0,0,1,0],[1,-1,1,0],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];
    CONST robtarget MoveToHome:=[[410,0,450],[0,0,-1,0],[0,0,-1,0],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];
    ! Offset for the end-effector
    CONST num offSet := 125;
    !Dummy Array for Testing
    CONST num qwirkleArray{4,7} := [[0,409,22,175,0,147,45],[0,409,22,548.6,0,147,0],[0,409,22,175,520,147,-45],[0,409,22,175,-520,147,-45]];
    
    PROC Main()
        MoveL MoveToHome,v1000,z100,tool0\WObj:=wobj0;
        decorationProcess qwirkleArray;
        MoveL MoveToHome,v1000,z100,tool0\WObj:=wobj0;
    ENDPROC
    
    PROC decorationProcess(num array{*,*})
        
        VAR num xStart;
        VAR num yStart;
        VAR num zStart;
        VAR num xEnd;
        VAR num yEnd;
        VAR num zEnd;
        VAR num orientation;
        VAR robtarget qwirkleLocation;
        VAR robtarget qwirklePlacement;
        
        FOR a FROM 1 TO Dim(array,1) DO
            ! This part reads in the first line.
            xStart := array{a,1};            
            yStart := array{a,2};
            zStart := array{a,3}+offSet;
            xEnd := array{a,4};            
            yEnd := array{a,5};
            zEnd := array{a,6}+offSet;
            orientation := array{a,7};
            
            qwirkleLocation := [[xStart,yStart,zStart],[0,0,1,0],[0,0,0,0],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];
            qwirklePlacement :=[[xEnd,yEnd,zEnd],[0,0,1,0],[0,0,0,0],[9E+09,9E+09,9E+09,9E+09,9E+09,9E+09]];

            PathToConvFromHome;
            MoveL qwirkleLocation,v1000,z100,tool0\WObj:=wobj0;
            !turnOnVacuum;
            !turnOnGripper;
            ResetConveyorPath;
            PathToHomeFromConv;
            WaitTime 0.5;
            MoveL qwirklePlacement,v1000,z100,tool0\WObj:=wobj0;
            RotateTool orientation,qwirklePlacement;
            IF (yEnd < -400) THEN
                LeftSidePath;
            ELSE
                MoveL MoveToHome,v1000,z100,tool0\WObj:=wobj0;
            ENDIF
                
        ENDFOR
          
    ENDPROC
    
    PROC InitialiseStart()
        MoveL MoveToHome,v1000,z100,tool0\WObj:=wobj0;
    ENDPROC
    
    PROC PathToConvFromHome()
            MoveL ImmediatePoint1,v1000,z100,tool0\WObj:=wobj0;
            MoveL ImmediatePoint2,v1000,z100,tool0\WObj:=wobj0;
            MoveL ImmediatePoint3,v1000,z100,tool0\WObj:=wobj0;    
    ENDPROC
    
    PROC PathToHomeFromConv()       
        MoveL ImmediatePoint3,v1000,z100,tool0\WObj:=wobj0;
        MoveL ImmediatePoint2,v1000,z100,tool0\WObj:=wobj0;
        MoveL ImmediatePoint1,v1000,z100,tool0\WObj:=wobj0;
    ENDPROC
    PROC ResetConveyorPath()
        MoveL ImmediatePoint3,v1000,z100,tool0\WObj:=wobj0;
    ENDPROC
    PROC ResetTablePath()
        MoveL ImmediatePoint1,v1000,z100,tool0\WObj:=wobj0;
    ENDPROC

    PROC RotateTool(num Orientation,robtarget a)
        MoveL RelTool(a,0,0,0,\Rz:= Orientation),v1000,fine,tool0\WObj:=wobj0;
        !WaitTime 2;
        !turnOffGripper;
        !turnOffVacuum;
        WaitTime 1;
        
    ENDPROC
    
    PROC LeftSidePath()
       
        MoveL ImmediatePoint4,v1000,z100,tool0\WObj:=wobj0;
        MoveL ImmediatePoint5,v1000,z100,tool0\WObj:=wobj0;
        MoveL MoveToHome,v1000,z100,tool0\WObj:=wobj0;
        
        
    ENDPROC
    
ENDMODULE
